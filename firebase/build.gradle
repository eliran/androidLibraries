apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def firebaseVersion = '11.8.0'
def module_version = '0.1.4'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-storage:$firebaseVersion"
    compile "com.google.firebase:firebase-config:$firebaseVersion"
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"
    compile project(path: ':android')
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar){
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc){
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc){
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.threeplay.libraries'
version = module_version

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Firebase extensions'
            description 'Threeplay firebase extensions'
            url = 'https://github.com/eliran/androidLibraries.git'
            inceptionYear = '2016'
            packaging = 'aar'
            groupId = 'com.threeplay.libraries'
            artifactId = 'firebase'
            version = module_version

            licenses {
                license {
                    name "The Apache Software License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution "repo"
                }
            }

            developers {
                developer {
                    name 'Eliran Ben Ezra' //developer name
                    email 'eliran@threeplay.com'
                }
            }
            scm {
                connection 'https://github.com/eliran/androidLibraries.git'
                url 'https://github.com/eliran/androidLibraries'
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'com.threeplay.libraries'
        name = 'firebase'
        userOrg = 'threeplay'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/eliran/androidLibraries.git'
        publish = true
        version {
            name = module_version
            desc = 'Firebase extensions'
            released = new Date()
            vcsTag = module_version
        }
    }
}

